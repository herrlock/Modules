
apply plugin: 'java'
sourceCompatibility = '1.7'

apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'

findbugs {
	ignoreFailures = true
	effort = 'max'
	reportLevel = 'low'
}
tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

pmd {
	ignoreFailures = true
	ruleSetFiles = files("${rootProject.projectDir}/misc/pmd.rulesets.xml")
}

apply plugin: 'maven-publish'
task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}
assemble.dependsOn sourceJar

task javadocJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.outputs
    classifier 'javadoc'
}
assemble.dependsOn javadocJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

dependencies {
	if(name != 'log4j') {
		compile project(':log4j')
	}
	
	testCompile 'junit:junit:4.12'
}
